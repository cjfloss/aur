# Maintainer: Frederik Schwan <freswa at archlinux dot org>
# Contributor: Sebastian BÃ¸e <sebastianbooe at gmail dot com>

pkgname=chisel
pkgver=3.3.1
pkgrel=1
pkgdesc='Constructing Hardware in a Scala Embedded Language'
arch=('any')
url='https://chisel.eecs.berkeley.edu/'
license=('BSD')
depends=('java-environment' 'verilator')
makedepends=('sbt' 'graphviz')
source=("https://github.com/freechipsproject/chisel3/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
b2sums=('0c98d31e6eb98a6f29487cb68fc73acf53b6c5cb373acdcd17bb8db9346d88fde2a0f3f366310d8a23bb97fba3f44228147deaaa2d58035efaf5dc27bea3f09d')

build(){
  cd ${pkgname}3-${pkgver}
  sbt publishLocal
}

package() {
  cd ${pkgname}3-${pkgver}
  install -dm755 "${pkgdir}"/usr/share/licenses/${pkgname} "${pkgdir}"/usr/share/scala/${pkgname}/
  install -Dm644 target/scala-2.12/${pkgname}3_2.12-${pkgver}.jar "${pkgdir}"/usr/share/scala/${pkgname}/${pkgname}.jar
  install -Dm644 src/LICENSE.txt "${pkgdir}"/usr/share/licenses/${pkgname}/
}
